title touch
.model small
.386
.stack 100h
.data
    filespec db 100 dup(0)
    dta db 43 dup(0)
    msg db "touch utility 0.9, David J. Burger",'$'
    CRLF db 0Ah, 0Dh, '$'
    fileHandle dw ?
    year dw ?
    month db ?
    day db ?
    hour db ?
    minutes db ?
    seconds db ?
    filenameOffset dw ?
.code
main proc
    ;mov PSP to bx for Get_Commandtail
    mov bx, ds
    mov ax, @data
    mov ds, ax
    mov es, ax
    mov dx, offset filespec  ;input parameter
    call Get_Commandtail
    
    ;set up filenameOffset so that it contains
    ;the count from the beginning of filespec
    ;to where the actual filename part is,
    ;for example, given:
    ;touch c:\whatever\*.*
    ;filenameOffset = 12
    ;this is so we can operate on files not
    ;in the current directory by appending the
    ;found filename to the path when using open
    ;to get the file handle
    mov ax,seg filespec
    mov es,ax
    mov di,offset filespec + 99
    mov cx,100
    mov al,'\'
    std
    repne scasb
    jnz CurrentDirectory
    ;always pointing one in front of where found
    ;add two to skip to real filename part
    add di,2
    mov filenameOffset,di
    jmp SetUpDTA
    
CurrentDirectory:
    mov filenameOffset,0
    
SetUpDTA:
    ;set up the dta
    mov ah,1Ah
    mov dx, offset dta
    int 21h
    
    ;store the date
    mov ah,2Ah
    int 21h
    sub cx, 1980
    mov year,cx
    mov month,dh
    mov day,dl
    
    ;store the time
    mov ah,2Ch
    int 21h
    mov hour,ch
    mov minutes,cl
    mov seconds,dh
    
    ;very strange, if I don't print this
    ;this routine won't work correctly
    ;when given a path    
    ;CRLF
    mov ah,40h
    mov bx,1
    mov dx,offset CRLF
    mov cx,2
    int 21h
        
    ;find the first file
    mov ah, 4Eh
    mov cx, 0
    mov dx, offset filespec
    int 21h
    jc Exit
Loop1:
    ;copy the filename to the filespec
    ;position of the filename in case
    ;not in current directory
    mov cx,13
    cld
    mov si,offset dta + 30
    mov di,offset filespec
    add di,filenameOffset
    rep movsb
    ;di points one byte beyond destination
                
    ;open the file
    mov ah,3Dh
    mov al,0
    ;using the filespec with the filename copied in    
    mov dx,offset filespec
    int 21h
    jc Exit
    mov fileHandle,ax    
                
    ;set up the date
    mov ax,0
    mov bx,0
    mov dx,0
    mov al,day
    mov bl,month
    shl bx,5
    mov dx,year
    shl dx,9
    or dx,ax
    or dx,bx
    
    ;set up the time
    mov ax,0
    mov bx,0
    mov cx,0
    mov al,seconds
    mov bl,minutes
    shl bx,5
    mov cl,hour
    shl cx,11
    or cx,ax
    or cx,bx
    
    ;set the date/time
    mov ah,57h
    mov al,1
    mov bx, fileHandle    
    int 21h
        
    ;find the next file
    mov ah,4Fh
    int 21h
    jc Exit
    jmp Loop1
Exit:
    mov ax, 4C00h
    int 21h    
main endp


Get_Commandtail proc
    pusha
    mov es,bx
    mov si,dx
    mov di,81h
    mov cx,0
    mov cl,es:[di-1]
    cmp cx,0
    je L2
    mov al,20h
    repz scasb
    jz L2
    dec di
    inc cx
    
L1: mov al,es:[di]
    mov [si],al
    inc si
    inc di
    loop L1
    clc
    jmp L3
    
L2: stc
L3: popa
    ret
Get_Commandtail endp

end main
